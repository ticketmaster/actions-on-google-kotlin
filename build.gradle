allprojects {
    group 'com.ticketmaster.actions'
    version '2.0.4'

    apply plugin: 'idea'
    idea {
        module {
            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }
    if (project.convention.findPlugin(JavaPluginConvention)) {
        // Change the output directory for the main and test source sets back to the old path
        sourceSets.main.output.classesDir = new File(buildDir, "classes/main")
        sourceSets.test.output.classesDir = new File(buildDir, "classes/test")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

task cleanOutDir(type: Delete) {
    delete 'out'
}

repositories {
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

buildscript {
    ext.kotlinVersion = '1.2.51'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "com.eriwen:gradle-js-plugin:2.14.1"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

ext {
    sdkVersion = '2.0.4' //version of deployed artifacts
    nodeVersion = '8.11.1'
    qunitVersion = '2.6.0'
    libraries = [
            junit                         : "junit:junit:4.12",
            kotlin_stdlib                 : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
            kotlin_stdlib_common          : "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion",
            kotlin_stdlib_js              : "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion",
            kotlin_test_annotations_common: "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion",
            kotlin_test_common            : "org.jetbrains.kotlin:kotlin-test-common:$kotlinVersion",
            kotlin_test_js                : "org.jetbrains.kotlin:kotlin-test-js:$kotlinVersion",
            kotlin_test_junit             : "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion",
    ]
}

subprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}
